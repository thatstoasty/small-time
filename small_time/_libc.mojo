import std.sys._libc as libc
from std.ffi import c_char, c_int, c_long, c_uchar, external_call, get_errno
from std.sys import CompilationTarget


comptime time_t = Int64
"""C `time_t` type, representing time in seconds since the Epoch (1970-01-01 00:00:00 UTC)."""
comptime suseconds_t = time_t
"""C `suseconds_t` type, representing microseconds. It is typically the same as `time_t`."""
comptime c_void = UInt8
"""C `void` type, used for generic pointers."""

comptime ImmutExternalUnsafePointer = UnsafePointer[origin=ImmutExternalOrigin, ...]
comptime MutExternalUnsafePointer = UnsafePointer[origin=MutExternalOrigin, ...]


@fieldwise_init
struct _CTimeValue(ImplicitlyCopyable, TrivialRegisterPassable):
    """C `TimeValue` struct."""

    var seconds: time_t
    """Seconds to wait. Corresponds to `tv_sec` in C."""
    var microseconds: suseconds_t
    """Microseconds to wait. Corresponds to `tv_usec` in C."""


@fieldwise_init
struct _CTimeZone(ImplicitlyCopyable, TrivialRegisterPassable):
    """C `timezone` struct."""

    var minutes_west: c_int
    """Minutes west of Greenwich."""
    var dst_time_correction: c_int
    """Type of DST correction."""


@fieldwise_init
struct _CTime(ImplicitlyCopyable, Writable):
    """C `tm` struct."""

    var seconds: c_int
    """Seconds, valid range is 0-60 (60 is for leap seconds)."""
    var minutes: c_int
    """Minutes, valid range is 0-59."""
    var hours: c_int
    """Hour, valid range is 0-23."""
    var day_of_month: c_int
    """Day of the month, valid range is 1-31."""
    var month: c_int
    """Month, valid range is 0-11 (0 is January, 11 is December)."""
    var year: c_int
    """Year minus 1900."""
    var day_of_week: c_int
    """Day of the week, valid range is 0-6 (0 is Sunday)."""
    var day_of_year: c_int
    """Day of the year, valid range is 0-365 (Jan/01 = 0)."""
    var is_daylight_savings: c_int
    """Whether daylight saving time is in effect at the time described.
    The value is positive if daylight saving time is in effect,
    zero if it is not, and negative if the information is not available."""
    var time_zone_offset: c_long
    """The difference, in seconds, of the timezone represented by this broken-down time and UTC"""
    var time_zone: ImmutExternalUnsafePointer[c_char]
    """Pointer to a string representing the timezone name, e.g. "UTC", "America/New_York"."""

    fn __init__(out self):
        """Initializes a new time struct."""
        self.seconds = 0
        self.minutes = 0
        self.hours = 0
        self.day_of_month = 0
        self.month = 0
        self.year = 0
        self.day_of_week = 0
        self.day_of_year = 0
        self.is_daylight_savings = 0
        self.time_zone_offset = 0
        self.time_zone = ImmutExternalUnsafePointer[c_char]()

    fn write_to(self, mut writer: Some[Writer]):
        """Writes the time struct to a writer.

        Args:
            writer: The writer to write to.
        """
        writer.write(
            "tm(seconds=",
            self.seconds,
            ", minutes=",
            self.minutes,
            ", hours=",
            self.hours,
            ", day_of_month=",
            self.day_of_month,
            ", month=",
            self.month,
            ", year=",
            self.year,
            ", day_of_week=",
            self.day_of_week,
            ", day_of_year=",
            self.day_of_year,
            ", is_daylight_savings=",
            self.is_daylight_savings,
            ", time_zone_offset=",
            self.time_zone_offset,
        )
        if self.time_zone:
            writer.write(", time_zone=", StringSlice(unsafe_from_utf8_ptr=self.time_zone))
        writer.write(")")


fn _gettimeofday(tv: MutUnsafePointer[_CTimeValue, ...], tz: MutUnsafePointer[_CTimeZone, ...]) -> c_int:
    """Gets the current time. It's a wrapper around libc `gettimeofday`.
    The `tv` parameter is a pointer to a `struct timeval` that will be filled.

    Args:
        tv: UnsafePointer to a `struct timeval` that will be filled with the current time.
        tz: UnsafePointer to a `struct timezone` that will be filled with the timezone information.

    Returns:
        The return value is 0 on success, or -1 on error. If an error occurs,
        the global variable `errno` is set to indicate the error.

    #### C Function:
    ```c
    int gettimeofday(struct timeval *restrict tv, struct timezone *_Nullable restrict tz);
    ```
    """
    return external_call["gettimeofday", c_int, type_of(tv), type_of(tz)](tv, tz)


fn get_time_of_day() raises -> _CTimeValue:
    """Gets the current time. Wrapper around libc `gettimeofday`.

    Returns:
        The current time.

    #### C Function:
    ```c
    int gettimeofday(struct timeval *restrict tv, struct timezone *restrict tz);
    ```
    """
    var tv = InlineArray[_CTimeValue, 1](uninitialized=True)
    var tz = InlineArray[_CTimeZone, 1](uninitialized=True)
    var result = _gettimeofday(tv.unsafe_ptr(), tz.unsafe_ptr())
    if result != 0:
        var errno = get_errno()
        if errno == errno.EFAULT:
            raise Error(
                "[EFAULT] gettimeofday failed: One of `tv` or `tz` pointed outside the accessible address space."
            )
        else:
            raise Error("[UNKNOWN] gettimeofday failed with unknown errno code: ", errno)
    return tv[0].copy()


fn _localtime_r(timep: ImmutUnsafePointer[time_t, ...], result: MutUnsafePointer[_CTime, ...]) -> None:
    """Converts a time value to a broken-down local time.

    Args:
        timep: UnsafePointer to a time value in seconds since the Epoch.
        result: UnsafePointer to a `_CTime` struct where the broken-down local time will be stored.

    #### C Function:
    ```c
    struct tm *localtime_r(const time_t *timep, struct tm *result);
    ```
    """
    _ = external_call["localtime_r", ImmutExternalUnsafePointer[_CTime], type_of(timep), type_of(result)](timep, result)


fn get_local_time(seconds_since_epoch: time_t) raises -> _CTime:
    """Converts a time value to a broken-down local time.

    Args:
        seconds_since_epoch: Time value in seconds since the Epoch.

    #### C Function:
    ```c
    struct tm *localtime_r(const time_t *timep, struct tm *result);
    ```
    """
    var result = InlineArray[_CTime, 1](uninitialized=True)
    _localtime_r(UnsafePointer(to=seconds_since_epoch), result.unsafe_ptr())
    if not result.unsafe_ptr():
        raise Error(
            "get_local_time failed: The pointer to the result is still null, which indicates the conversion failed."
        )
    return result[0].copy()


fn _strptime(
    buf: ImmutUnsafePointer[c_char, ...], format: ImmutUnsafePointer[c_char, ...], tm: MutUnsafePointer[_CTime, ...]
) -> MutExternalUnsafePointer[c_char]:
    """Parses a time string according to a format string.

    Args:
        buf: Time string to parse.
        format: Time format string.
        tm: UnsafePointer to a `_CTime` struct where the broken-down time will be stored.

    Returns:
        Broken down time.

    #### C Function:
    ```c
    char *strptime(
        const char *restrict buf, const char *restrict format, struct tm *restrict tm
    );
    ```
    """
    return external_call[
        "strptime",
        MutExternalUnsafePointer[c_char],
        type_of(buf),
        type_of(format),
        type_of(tm),
    ](buf, format, tm)


fn parse_time_with_format(mut time: String, mut format: String) raises -> _CTime:
    """Parses a time string according to a format string.

    Args:
        time: Time string to parse. This must be mutable so it can be null terminated for C interop.
        format: Time format string. This must be mutable so it can be null terminated for C interop.

    Returns:
        Broken down time.

    #### C Function:
    ```c
    char *strptime(
        const char *restrict buf, const char *restrict format, struct tm *restrict tm
    );
    ```
    """
    var tm = InlineArray[_CTime, 1](uninitialized=True)
    _ = _strptime(
        time.as_c_string_slice().unsafe_ptr(),
        format.as_c_string_slice().unsafe_ptr(),
        tm.unsafe_ptr(),
    )
    if not tm.unsafe_ptr():
        raise Error(
            "parse_time_with_format failed: The pointer to the result is still null, which indicates the parsing"
            " failed."
        )

    return tm[0].copy()


fn _gmtime(timep: ImmutUnsafePointer[time_t, ...]) -> MutExternalUnsafePointer[_CTime]:
    """Converts a time value to a broken-down UTC time.

    Args:
        timep: UnsafePointer to a time value in seconds since the Epoch.

    Returns:
        Broken down UTC time.

    #### C Function:
    ```c
    struct tm *gmtime(const time_t *timep);
    ```
    """
    return external_call["gmtime", MutExternalUnsafePointer[_CTime], type_of(timep)](timep)


fn get_gm_time(time: time_t) raises -> _CTime:
    """Converts a time value to a broken-down UTC time.

    Args:
        time: Time value in seconds since the Epoch.

    Returns:
        Broken down UTC time.

    #### C Function:
    ```c
    struct tm *gmtime(const time_t *timep);
    ```
    """
    var result = _gmtime(UnsafePointer[mut=False](to=time))
    if not result:
        raise Error(
            "get_gm_time failed: The pointer to the result is still null, which indicates the conversion failed."
        )

    # TODO (Mikhail): Maybe copy the result, not sure if take_pointee is safe here.
    return result.take_pointee()
